# Process this file with autom4te to create testsuite. -*- Autotest -*-

# Test suite for GNU Radius.
# Copyright (C) 2004-2025 Free Software Foundation, Inc.

# GNU Radius is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3, or (at your option)
# any later version.

# GNU Radius is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with GNU Radius.  If not, see <http://www.gnu.org/licenses/>.

# We need a recent Autotest.
m4_version_prereq([2.52g])

AT_INIT

AT_TESTED([radiusd radtest])

m4_define([AT_SKIP_TEST],[exit 77])

m4_define([RA_CHECK],[
 AT_DATA([script],[
$1
])
 AT_CHECK([
 if test -r "$BUILDDIR/confdir"; then
    RADIUSD_CONFDIR=`cat "$BUILDDIR/confdir"`
 else
    echo "confdir ($BUILDDIR/confdir) not readable"
    AT_SKIP_TEST
 fi
 if test -d "$RADIUSD_CONFDIR"; then :; else
    echo "RADIUSD_CONFDIR ($RADIUSD_CONFDIR) is not a directory"
    AT_SKIP_TEST
 fi
 radtest -d "$RADIUSD_CONFDIR" --no-interactive -f script],
  [0],
  [m4_if([$2],[],[PASS
],[$2])],
  [$3],
  [catlog $LOCAL_CONF/log radius.log radius.debug radius.stderr
catlog $PROXY_CONF/log radius.log radius.debug radius.stderr
])
])

m4_define([RA_PREREQ],
 [AT_CHECK([radiusd --show-defaults|grep "$1" || AT_SKIP_TEST],
	   [0],
	   [$1
],
	   [],
	   [AT_SKIP_TEST],
	   [$2])])

m4_define([RA_ALL_KEYWORDS],[status-server basic-auth reply begin-kw default-kw match-profile hints huntgroups scheme-auth acl expiration menu acct ext-acct rewrite execwait filter scheme-acct tunnel log-mode])

m4_define([RA_PROXY_KEYWORDS],[proxy-basic-auth proxy-ignorecase proxy-vsa proxy-menu proxy-nested proxy-acct proxy-tunnel])

##

m4_include([begin.at])

m4_foreach([keyword],
	   m4_split(RA_ALL_KEYWORDS,[[
	 ]]+),
	   [m4_include(keyword[.at])])

m4_include([end.at])

m4_include([begin-proxy.at])

m4_foreach([keyword],
	   m4_split(RA_PROXY_KEYWORDS,[[
	 ]]+),
	   [m4_include(keyword[.at])])

m4_include([end-proxy.at])

# End of testsuite.at
